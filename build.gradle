plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group 'davidlum'
version '1.0'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
  implementation group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
  implementation group: 'net.sf.supercsv', name: 'super-csv', version: '2.4.0'
  
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

shadowJar {
   baseName = 'capitect-all'
   classifier = null
   version = null
}

mainClassName = 'davidlum.capitectReport.ui.SwingPicker'

def embeddedJRE = true

task javapackager(type: Exec, dependsOn: [assemble,shadowJar]) {
  def nativeType
  if (System.properties['os.name'].toLowerCase().contains('windows'))
  nativeType = 'msi'
  if (System.properties['os.name'].toLowerCase().contains('mac'))
  nativeType = 'pkg' // 'dmg'
  if (System.properties['os.name'].toLowerCase().contains('linux'))
  nativeType = 'rpm'
  
  def dependencies = []
  configurations.runtime.forEach({ file ->
      dependencies.add('-srcfiles')
      dependencies.add(file.getName())
    })
  
  def paramEmbeddedJRE = embeddedJRE ? [] : ['-Bruntime=']
  
  workingDir project.projectDir
  commandLine = [
    'javapackager',
    '-deploy',
    '-Bicon=src/main/resources/capitectReportLogo.icns',
    '-nosign',
    '-native', nativeType,
    '-outdir', "${buildDir}/distribution",
    '-outfile', project.name,
    '-name', 'Capitect Reporter',
    '-appclass', mainClassName,
    '-srcdir', libsDir,
    '-srcfiles', shadowJar.archiveName
  ] + dependencies + paramEmbeddedJRE
}
